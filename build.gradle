/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'victor.caldentey' at '1/10/15 11:42' with Gradle 2.3
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at http://gradle.org/docs/2.3/userguide/tutorial_java_projects.html
 */

// Apply the java plugin to add support for Java
plugins {
    id 'java'
    id 'maven-publish'
    id 'pl.allegro.tech.build.axion-release' version '1.3.2'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    mavenLocal()
    maven {
        credentials {
            username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: ''
            password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: ''
        }

        url "http://artifactory.scmspain.com/artifactory/libs-release-ms"
    }
    mavenCentral()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    compile 'org.apache.kafka:kafka_2.10:0.8.2.2'
    compile 'com.netflix.archaius:archaius-core:0.6.3'
    compile 'io.reactivex:rxjava:1.0.+'
    compile 'com.google.inject:guice:3.0'    // This dependency is optional, but we are unable to say this in gradle in an easy way

    testCompile 'junit:junit:4.12'
    testCompile 'com.scmspain.karyon:karyon-rest-router:1.2.+'
    testCompile 'com.netflix.karyon:karyon2-core:2.7.1'
    testCompile 'com.netflix.karyon:karyon2-archaius:2.7.1'
    testCompile 'javax.ws.rs:jsr311-api:1.1.1'

}

tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from Javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Task publishMavenJavaPublicationToMavenRepository is added in runtime so doFirst doesn't work propertly and must be done this way
tasks.whenTaskAdded { t ->
    if( t.name == "publishMavenJavaPublicationToMavenRepository"){
        if (!project.hasProperty('artifactory_scm_user') || !project.hasProperty('artifactory_scm_password')) {
            throw new GradleException("Properties 'artifactory_scm_user' and 'artifactory_scm_password' are mandatory to do a publish, please add this information in your ~/.gradle/gradle.properties")
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId 'com.scmspain'
            version project.version

            from components.java

            Artifact sourcesJar
            Artifact javadocJar

        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty('artifactory_scm_user')? artifactory_scm_user: ''
                password project.hasProperty('artifactory_scm_password')? artifactory_scm_password: ''
            }

            url "http://artifactory.scmspain.com/artifactory/libs-release-ms"
        }
    }
}